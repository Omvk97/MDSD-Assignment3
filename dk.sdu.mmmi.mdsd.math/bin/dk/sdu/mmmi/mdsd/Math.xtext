grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

Program:
	'program' name=ID (externalMethods+=ExternalMethod)* math=MathExp
;

MathExp:
	variables += VarBinding*
;

VarBinding:
	'var' name=ID '=' expression=Exp
;

Exp returns Expression:
	Factor (( {Plus.left=current} '+' | {Minus.left=current}  '-' ) right=Factor)*
;

Factor returns Expression:
	Primary (({Mult.left=current} '*' | {Div.left=current}  '/'  ) right=Primary)*
;

Primary returns Expression:
	{MathNumber} value=INT  | Parenthesis | VariableUse | LetBinding | ExternalMethodCall
;

Parenthesis returns Expression:
	{Parenthesis} '(' expression=Exp ')'
;

ExternalMethod:
	'external' name=ID '(' types+=ID? (',' types+=ID)* ')'
;

ExternalMethodCall returns Expression:
	{ExternalMethodCall} method = [ExternalMethod] '(' (arguments+=Exp (',' arguments+=Exp)*)? ')'
;

LetBinding:
	'let' name=ID '=' binding=Exp 'in' body=Exp 'end'
;

Binding:
	VarBinding | LetBinding
;

VariableUse:
	ref = [Binding]
;
